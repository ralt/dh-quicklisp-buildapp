#!/usr/bin/perl -w

=head1 NAME

dh_quicklisp_buildapp_build - build the Lisp system using quicklisp

=cut

use strict;
use Debian::Debhelper::Dh_Lib;
use Cwd qw();

use QuicklispBuildapp::Utils;

init();

# The build is done in 4 parts:
#   - Get quicklisp.lisp
#   - Install quicklisp in .quicklocal/
#   - Install dependencies using the local quicklisp
#   - Build the binary using buildapp
my $cwd = Cwd::cwd();
doit("wget", "https://beta.quicklisp.org/quicklisp.lisp");
complex_doit("echo", "'4a7a5c2aebe0716417047854267397e24a44d0cce096127411e9ce9ccfeb2c17", "quicklisp.lisp'", "|", "sha256sum", "-c", "-");

complex_doit("sbcl", "--noinform", "--noprint", "--disable-debugger",
             "--no-sysinit", "--no-userinit", "--load", "quicklisp.lisp",
             "--eval", "'(quicklisp-quickstart:install", ":path", "\".quicklocal/quicklisp/\"",
             ")'", "--eval", "'(quit)'");

sub build_system {
    my ($binary, $system, $entrypoint) = @_;

    complex_doit("sbcl", "--noinform", "--noprint", "--disable-debugger",
                 "--no-sysinit", "--no-userinit",
                 "--load", ".quicklocal/quicklisp/setup.lisp",
                 "--eval", "'(push", "\"$cwd/\"", "asdf:*central-registry*)'",
                 "--eval", "'(ql:quickload", ":$system)'",
                 "--quit");
    complex_doit("buildapp", "--load", ".quicklocal/quicklisp/setup.lisp",
                 "--asdf-tree", ".quicklocal/quicklisp/dists",
                 "--asdf-path", ".", "--load-system", $system,
                 "--compress-core", "--output", $binary,
                 "--entry", $entrypoint);
}

QuicklispBuildapp::Utils::buildapp(@{$dh{DOPACKAGES}}, \&build_system);

=head1 SEE ALSO

L<debhelper(1)>

This program is part of the dh-quicklisp-buildapp package.

L<dh-quicklisp-buildapp(8)>

=head1 AUTHOR

Florian Margaine <florian@margaine.com>

=cut
